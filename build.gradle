plugins {
    id 'idea'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '5.1.5'
}

group 'net.chrotos.rpgapi'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        url "https://artifactory.chrotos.net/artifactory/public"
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url "https://repo.citizensnpcs.co/"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    compileOnly 'net.chrotos.chrotoscloud:api:3.0-SNAPSHOT'
    compileOnly 'net.chrotos.chrotoscloud:persistence-models:3.0-SNAPSHOT'
    compileOnly 'net.chrotos.chrotoscloud:core:3.0-SNAPSHOT'
    compileOnly 'net.chrotos.chrotoscloud:plugin-paper:3.0-SNAPSHOT'

    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'

    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.11'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.32-SNAPSHOT'
}

test {
    useJUnitPlatform()
}

artifactory {
    contextUrl = "${chrotos_contextUrl}"
    publish {
        contextUrl = "${chrotos_contextUrl}"
        repository {
            repoKey = 'public'
            username = "${chrotos_user}"
            password = "${chrotos_password}"
        }
        defaults {
            publications('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            artifactId = 'plugin'
            from shadow.component(publication)
        }
    }
}

shadowJar {
    classifier = null
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

task installLocalGitHook(type: Copy){
    from new File(rootProject.rootDir, '.scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775
}

build.dependsOn installLocalGitHook