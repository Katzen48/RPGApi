plugins {
    id 'idea'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group 'net.chrotos.rpgapi'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        url "https://maven.pkg.github.com/Katzen48/chrotoscloud-v2"
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url "https://repo.citizensnpcs.co/"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    compileOnly 'net.chrotos.chrotoscloud:api:3.0-SNAPSHOT'
    compileOnly 'net.chrotos.chrotoscloud:persistence-models:3.0-SNAPSHOT'
    compileOnly 'net.chrotos.chrotoscloud:core:3.0-SNAPSHOT'

    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'

    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.5'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.29-SNAPSHOT'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Katzen48/RPGapi")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) { publication ->
            artifactId = 'plugin'
            from shadow.component(publication)
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

task installLocalGitHook(type: Copy){
    from new File(rootProject.rootDir, '.scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775
}

build.dependsOn installLocalGitHook